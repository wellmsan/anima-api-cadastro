plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'app.qualidade'
version = '0.0.1-SNAPSHOT'
description = 'api-cadastro'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'

    // Banco de dados
    runtimeOnly 'com.h2database:h2'

    // Testes
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    
}

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        showExceptions = true
        exceptionFormat = "full"
    }
}

// Task para construir o JAR otimizado para Docker
tasks.named('bootJar') {
    archiveFileName = 'app.jar'
    layered {
        enabled = true
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-parameters']
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    jvmArgs += ['-parameters']
}
